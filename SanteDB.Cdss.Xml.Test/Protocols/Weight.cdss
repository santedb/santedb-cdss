include <org.santedb.cdss.core>
include <org.santedb.cdss.core.childcare>

library "Childhood Healthy Growth"
	having id <org.santedb.cdss.growth>
	having uuid "063FFFAD-FC37-4666-B949-4A96AE2A4FB9"
	having status active
	with metadata
		author SanteSuite Inc. and the SanteSuite Contributors
		version 3.0
		doc This library provides decision logic for the computation of childhood health weight observations
		doc and provides the ability to analyse those results given the z-score of the weight and the child's
		doc age.
	end 
	as 
		logic "Childhood Growth Patient Logic"
			having id <org.santedb.cdss.growth.patient>
			having status active
			having context Patient when all("Patient Is a Child", "Patient Is Not Deceased")
			with metadata
				doc Contains logic related to a patient who is a child - proposes weight and height measurements once per month until the child reaches 5 years old
			end
			as
				fact "Earliest Date of Observation"  having type date as
					csharp($$ context.Target.DateOfBirth.Value.Date.AddMonths((int)context["monthNo"]) $$)
				end

				fact "Latest Date of Observation" having type date as 
					csharp($$ context.Target.DateOfBirth.Value.Date.AddMonths((int)context["monthNo"] + 1).AddDays(-1).AddHours(23).AddMinutes(59).AddSeconds(59) $$)
				end

				fact "No Weight Has Been Recorded for Period" having type bool as 
					hdsi($$ 
							participation[RecordTarget].source.typeConcept.mnemonic=VitalSign-Weight
							&participation[RecordTarget].source.actTime=>$Earliest Date Of Observation
							&participation[RecordTarget].source.actTime=<$Latest Date Of Observation
						$$)
				end 

				fact "No Height Has Been Recorded for Period" having type bool as 
					hdsi($$ 
							participation[RecordTarget].source.typeConcept.mnemonic=VitalSign-Height
							&participation[RecordTarget].source.actTime=>$Earliest Date Of Observation
							&participation[RecordTarget].source.actTime=<$Latest Date Of Observation
						$$)
				end 

				model "Measure Weight Proposal"
				as 
					$$
					{
						"$type": "QuantityObservation",
						"templateModel": {
							"mnemonic": "org.santedb.emr.observation.weight",
							"oid": "1.3.6.1.4.1.52820.5.2.1.10"
						},
						"moodConcept": "ACF7BAF2-221F-4BC2-8116-CEB5165BE079",
						"classConcept": "28D022C6-8A8B-47C4-9E6A-2BC67308739E",
						"typeConceptModel" : {
							"id": "a261f8cd-69b0-49aa-91f4-e6d3e5c612ed",
							"mnemonic":"VitalSign-Weight"
						},
						"unitOfMeasure" : "a0a8d4db-db72-4bc7-9b8c-c07cef7bc796"
					}
					$$
				end

				model "Measure Height Proposal"
				as 
					$$
					  {
						"$type": "QuantityObservation",              
						"templateModel": {
						"mnemonic": "org.santedb.emr.observation.heightLying",
							"oid": "1.3.6.1.4.1.52820.5.2.1.6"
						},
						"moodConcept": "ACF7BAF2-221F-4BC2-8116-CEB5165BE079",
						"classConcept": "28D022C6-8A8B-47C4-9E6A-2BC67308739E",
						"statusConceptModel" : { "id" : "c8064cbd-fa06-4530-b430-1a52f1530c27", "mnemonic" : "ACTIVE" },
						"typeConcept" : "850ca898-c656-4ba2-a7c1-ff74e3331396",
						"typeConceptModel": {
						"id":"850ca898-c656-4ba2-a7c1-ff74e3331396",
						"mnemonic":"VitalSign-Height"
						},
							"value" : 0,
						"unitOfMeasure" : "eeefb6bb-18b1-478d-9cc5-575325b947a8"
					}
					$$
				end

				rule "Should Measure Child Height"
					having id <org.santedb.cdss.growth.patient.rule.height>
				as 
					when "No Height Has Been Recorded for Period"
					then
						propose "Measure Height For Child"
							having model "Measure Height Proposal"
						as
							assign "Earliest Date of Observation" to startTime
							assign "Latest Date Of Observation" to stopTime
							assign csharp($$ context.Target.DateOfBirth.Value.AddMonths((int)context["monthNo"]) $$) to actTime
						end
				end

				rule "Should Measure Child Weight"
					having id <org.santedb.cdss.growth.patient.rule.weight>
				as 
					when "No Weight Has Been Recorded for Period"
					then
						propose "Measure Weight for Child"
							having model "Measure Weight Proposal"
						as
							assign "Earliest Date of Observation" to startTime
							assign "Latest Date Of Observation" to stopTime
							assign csharp($$ context.Target.DateOfBirth.Value.AddMonths((int)context["monthNo"])) $$) to actTime
						end
				end

				protocol "Measure Growth of Children Under 5 Years of Age Every Month"
					having id <org.santedb.cdss.growth.patient.protocol>
					having uuid "063FFFAD-FC37-4666-B949-4A96AE2A4FB9"
					having oid "1.3.5.1.4.1.52820.5.3.1.1"
				as
					when "Patient Is Child"
				then 
					repeat for 60 iterations track-by monthNo
						apply "Should Measure Child Weight"
						apply "Should Measure Child Height"
					end
				end 
		end logic // for patient

		logic "Interpret Weight Observation against computed Z-Scores"
			having id <org.santedb.cdss.growth.weight.obs>
			having status active
			having context QuantityObservation when hdsi("typeConcept=a261f8cd-69b0-49aa-91f4-e6d3e5c612ed")
		as 
			
			fact "Weight is Expressed in Lbs" having type bool
				with metadata
					doc For a quantity observation. indicates whether the observed value is expressed in imperial pounds
				end 
			as 
				hdsi("unitOfMeasure=49974584-7c48-457e-a79c-031cdd62714a")
			end 

			fact "Observed Weight Expressed in Kgs" having type real
				with metadata
					doc Converts the quantity observation to KGS if it is expressed in LBS
				end
			as 
				hdsi("value")
				normalize when "Weight is Expressed in Lbs" computed csharp($$ value * 0.45359237d $$)
			end

			fact "Patient Is Underweight" having type bool as 
				csharp($$ ((double)context["Observed Weight Expressed in Kgs"]) < (double)context.DataSets["#org.santedb.cdss.weight.zscores"].Lookup("gender", context["Record Target Gender"]).Lookup("age", context["Record Target Age in Days"]).Select("zMinus2") $$)
			end

			fact "Patient is Overweight" having type bool as
				csharp($$ ((double)context["Observed Weight Expressed in Kgs"]) > (double)context.DataSets["#org.santedb.cdss.weight.zscores"].Lookup("gender", context["Record Target Gender"]).Lookup("age", context["Record Target Age in Days"]).Select("zPlus2") $$)
			end

			// This rule will always fire since it has no WHEN condition and sets the default interpretation concept to normal
			rule "Set Default Interpretation of Weight to Normal" as
				then
					assign const "41D42ABF-17AD-4144-BF97-EC3FD907F57D" to interpretationConcept
			end

			rule "When Patient is Underweight - Set Interpretation to Low and Propose Vitamin Supplement" as
				when "Patient Is Underweight"
				then 
					assign const "6188F821-261F-420C-9520-0DE240A05661" to interpretationConcept
					raise having priority warn "Patient weight is abnormally low - Please provide appropriate nutritional counseling and check for conditions such as ringworm, cholera, etc."
					propose
						having model as
						$$
						{
							"$type": "SubstanceAdministration",
							"templateModel": {
								mnemonic: "org.santedb.emr.sbadm.supplement",
								"oid": "1.3.5.1.4.1.52820.5.2.3.1"
							},
							"moodConceptModel": { "id" : "ACF7BAF2-221F-4BC2-8116-CEB5165BE079", "mnemonic" : "PROPOSE" },
							"typeConceptModel": { "id" : "7D84A057-1FCC-4054-A51F-B77D230FC6D1", "mnemonic" : "DrugTherapy" },
							"doseQuantity" : 1.0,
							"doseUnit" : "a77b8d83-1cc9-4806-a268-5d1738154afa",
							"route" : "0a1388b0-66fb-4063-bfe3-151dd8442838",
							"site" : "e5b6847e-91e0-4fca-ac2e-753962008080",
							"participation": {
								"Product": [{
									"player" : "7D80E518-6E04-4091-8D19-7265BB09BC1F",
								}]
							}
						}
						$$
						end model
					as
						assign csharp("DateTimeOffset.Now") to actTime
					end
			end rule

			rule "When Patient is Overweight - Set Interpretation Concept and Instruct Nutritional Counseling"
			as
				when "Patient Is Overweight"
				then 
					assign const "3C4D6579-7496-4B44-AAC1-18A714FF7A05" to interpretationConcept
					raise having priority warn "Patient weight abnormally high - Please provide appropriate nutritional counseling" 
			end 
		end logic // observation

		data "WeightZScores For Children Under 5" 
			having id <org.santedb.cdss.weight.zscores>
		as
		$$
age,gender,zMinus4,zMinus3,zMinus2,zMinus1,Z0,zPlus1,zPlus2,zPlus3,zPlus4
0,Male,1.701,2.08,2.459,2.881,3.346,3.859,4.419,5.031,5.642
1,Male,1.692,2.065,2.437,2.854,3.317,3.83,4.394,5.013,5.633
2,Male,1.707,2.08,2.454,2.872,3.337,3.852,4.421,5.045,5.669
3,Male,1.725,2.1,2.475,2.895,3.363,3.881,4.453,5.083,5.712
4,Male,1.745,2.122,2.499,2.921,3.392,3.913,4.49,5.124,5.758
5,Male,1.767,2.146,2.525,2.949,3.422,3.947,4.528,5.167,5.806
6,Male,1.789,2.17,2.551,2.978,3.454,3.983,4.568,5.211,5.855
7,Male,1.812,2.195,2.579,3.009,3.488,4.02,4.609,5.257,5.905
8,Male,1.835,2.221,2.607,3.04,3.522,4.058,4.65,5.303,5.956
9,Male,1.859,2.248,2.637,3.072,3.558,4.097,4.693,5.351,6.009
10,Male,1.884,2.276,2.667,3.105,3.594,4.137,4.738,5.4,6.062
11,Male,1.91,2.304,2.698,3.14,3.632,4.179,4.784,5.451,6.118
12,Male,1.936,2.333,2.73,3.175,3.671,4.222,4.831,5.503,6.175
13,Male,1.963,2.363,2.764,3.212,3.711,4.266,4.88,5.558,6.235
14,Male,1.991,2.395,2.798,3.249,3.753,4.312,4.931,5.613,6.296
15,Male,2.02,2.426,2.833,3.288,3.796,4.359,4.983,5.671,6.358
16,Male,2.049,2.459,2.869,3.327,3.839,4.407,5.035,5.729,6.422
17,Male,2.078,2.491,2.905,3.367,3.883,4.455,5.089,5.787,6.486
18,Male,2.107,2.524,2.941,3.407,3.927,4.504,5.143,5.847,6.551
19,Male,2.137,2.557,2.977,3.448,3.971,4.553,5.196,5.906,6.615
20,Male,2.167,2.59,3.014,3.488,4.016,4.602,5.25,5.965,6.68
21,Male,2.197,2.624,3.051,3.528,4.06,4.651,5.304,6.024,6.745
22,Male,2.226,2.657,3.087,3.569,4.105,4.7,5.358,6.083,6.809
23,Male,2.256,2.69,3.124,3.609,4.149,4.748,5.411,6.142,6.873
24,Male,2.286,2.723,3.16,3.649,4.193,4.797,5.464,6.2,6.937
25,Male,2.316,2.756,3.197,3.689,4.237,4.845,5.517,6.259,7
26,Male,2.345,2.789,3.233,3.729,4.281,4.893,5.57,6.316,7.063
27,Male,2.375,2.822,3.269,3.768,4.324,4.94,5.622,6.373,7.125
		$$
		end data // for ref data
end