//
// Copyright (C) 2021 - 2023, SanteSuite Inc. and the SanteSuite Contributors (See NOTICE.md for full copyright notices)
// Copyright (C) 2019 - 2021, Fyfe Software Inc. and the SanteSuite Contributors
// Portions Copyright (C) 2015-2018 Mohawk College of Applied Arts and Technology
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you 
// may not use this file except in compliance with the License. You may 
// obtain a copy of the License at 
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the 
// License for the specific language governing permissions and limitations under 
// the License.
//
include <org.santedb.cdss.core>
include <org.santedb.cdss.core.childcare>

define library "Human Papilloma Virus (HPV)"
	having id <org.santedb.cdss.vaccine.hpv>
	having uuid {7c5b3cc2-ed0f-11ee-a1c2-972edb5910e4}
	having oid "1.3.5.1.4.1.52820.5.2.3.2.7"
	having status active
	with metadata
		version 3.07
		doc Administration of the HPV vaccine
	end metadata
	as

    define logic "HPV Vaccination Logic for Patient Context"
        having id <org.santedb.cdss.vaccine.hpv.patient>
        having status active
        having context Patient when all(
            "Patient Is a Child",
            "Patient Is Not Deceased", 
			"Patient Is Female"
        )
        as
        
        define fact "Patient's Last Administration of HPV Dose 1" as
            query(
            	from hdsi($$participation[RecordTarget]$$)
				where hdsi($$source@SubstanceAdministration.doseSequence=1&
                    source.participation[Product].player.typeConcept.mnemonic=VaccineType-HPV&
                    source.isNegated=false$$)
				select last hdsi($$source@SubstanceAdministration$$)
				order by hdsi($$source.actTime$$)
			)
		end fact
		
        define fact "Patient's Last Administration of HPV Dose 2" as
            query(
            	from hdsi($$participation[RecordTarget]$$)
				where hdsi($$source@SubstanceAdministration.doseSequence=2&
                    source.participation[Product].player.typeConcept.mnemonic=VaccineType-HPV&
                    source.isNegated=false$$)
				select last hdsi($$source@SubstanceAdministration$$)
				order by hdsi($$source.actTime$$)
			)
		end fact
        
        define fact "Patient Has an Unresolved AEFI to HPV Dose"
			with metadata
				doc Patient has an active (unresolved) AEFI to a previous dose of HPV
			end metadata
		as
			any(
				hdsi($$
                    participation[RecordTarget].source.typeConcept=0744B6AD-BE39-4A08-B64D-F61CB8282267
                    &participation[RecordTarget].source.relationship[HasSubject].target.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d
                    &participation[RecordTarget].source.relationship[RefersTo].target.participation[Product].player.typeConcept.mnemonic=VaccineType-HPV
                  $$),
				hdsi($$
                    participation[RecordTarget].source.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d&
                    participation[RecordTarget].source@CodedObservation.value.mnemonic=VaccineType-HPV&
                    participation[RecordTarget].source.isNegated=false
                  $$)
			)
		end fact
		
		define fact "Patient Refused Previous HPV Dose"
		    with metadata 
		        doc Patient has refused a previous HPV dose for a reason which should not be rescheduled
		    end
		    as
		    	hdsi($$
                  participation[RecordTarget].source.isNegated=true&
                  participation[RecordTarget].source.participation[Product].player.typeConcept.mnemonic=VaccineType-HPV&
        		  participation[RecordTarget].source.reasonConcept.conceptSet=3BD46185-E3C3-4A6D-A1FE-0F2C9C49360D
                $$)
		end fact 
		
		define fact "Patient Should Receive HPV Dose 1" type bool as 
		    none(
		        "Patient's Last Administration of HPV Dose 1",
		        "Patient Refused Previous HPV Dose"
		   )
		end fact
		
		define fact "Patient Should Receive HPV Dose 2" type bool as 
		    all(
		        "Patient's Last Administration of HPV Dose 1",
		        none(
		            "Patient's Last Administration of HPV Dose 2",
		            "Patient Refused Previous HPV Dose"
		        )
		  )
		end fact 
		
		define rule "Raise Alert if Active AEFI Reported for HPV" 
		as
		    when 
		        all(
		            any("Patient Should Receive HPV Dose 1", "Patient Should Receive HPV Dose 2"),
		            "Patient Has an Unresolved AEFI to HPV Dose"
		       )
		  then
		    raise having priority warn having type {1a4fff6c-f54f-11e8-8eb2-f2801f1b9fd1} $$
		        Patient has had a previous advers event and/or drug allergy/intolerance to HPV does - confirm adminsitration with patient/guardian prior to administration
		    $$
		end rule
		
		define model "HPV Dose Proposal" having format json as 
		$$
         {
        	"$type": "SubstanceAdministration",
            "template":"50ac9b2d-e560-4b75-ac77-921bf0eceee8",
            "templateModel" : {
                "mnemonic" : "org.santedb.emr.sbadm.immunization",
                "oid": "1.3.5.1.4.1.52820.5.2.3.2"
              },
              "typeConcept": "6e7a3521-2967-4c0a-80ec-6c5c197b2178",
              "doseQuantity" : 1.0,
              "doseUnit" : "a4fc5c93-31c2-4f87-990e-c5a4e5ea2e76",
              "route" : "d594f99f-0151-41a0-a359-282ab54683a1",
              "site" : "dd5db8ed-0d97-4728-bd94-27aacd79ea02",
        	  "participation": {
        		"Product": [{
                	"player" : "15b42b90-17fa-48cd-8121-f909c9d00ccc",
					"playerModel" : {
						"$type": "Material",
						"id" : "15b42b90-17fa-48cd-8121-f909c9d00ccc",
						"typeConceptModel": {
							"mnemonic": "VaccineType-HPV"
						}
					}
        		}]
        	}
        }
		$$
		end model
		
		
		define rule "Recommend Administration of HPV Dose 1" as 
		    when "Patient Should Receive HPV Dose 1"
		    then 
		        propose having model "HPV Dose Proposal"
		        as 
					assign csharp($$context.GreaterOf(DateTime.Now, context.Target.DateOfBirth.Value.AddMonths(108))$$) to actTime
		            assign csharp($$context.Target.DateOfBirth.Value.AddMonths(108)$$) to startTime
		            assign const 1 to doseSequence
		       end propose
		  end rule
		  
		  define rule "Recommend Administration of HPV Dose 2" as 
		    when "Patient Should Receive HPV Dose 2" 
		    then 
		        propose having model "HPV Dose Proposal"
		        as 
		            assign csharp($$((Act)context["Patient's Last Administration of HPV Dose 1"]).ActTime.Value.AddMonths(6)$$) to actTime 
		            assign hdsi($$actTime$$ scoped-to proposal) to startTime
		            assign const 2 to doseSequence
		       end propose
		  end rule
		  
		  define protocol "HPV Administration to Adolescents" 
		    having id <org.santedb.cdss.vaccine.hpv.protocol>
		    having uuid {EB23D992-53A9-4615-A781-1B8DED61863B}
		    having oid "1.3.5.1.4.1.52820.5.2.3.2.7.1"
		    having scope <org.santedb.ims.pediatric.routineVacc>
		  as 
		    when 
		        "Patient is a Child"
		    then 
		        apply "Recommend Administration of HPV Dose 1"
		        apply "Recommend Administration of HPV Dose 2"
		  end protocol
    end logic
	
	
end library
