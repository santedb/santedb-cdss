//
// Copyright (C) 2021 - 2023, SanteSuite Inc. and the SanteSuite Contributors (See NOTICE.md for full copyright notices)
// Copyright (C) 2019 - 2021, Fyfe Software Inc. and the SanteSuite Contributors
// Portions Copyright (C) 2015-2018 Mohawk College of Applied Arts and Technology
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you 
// may not use this file except in compliance with the License. You may 
// obtain a copy of the License at 
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the 
// License for the specific language governing permissions and limitations under 
// the License.
//
include <org.santedb.cdss.core>
include <org.santedb.cdss.core.childcare>
define library "PCV 13 Childhood Vaccination Schedule"
	having id <org.santedb.cdss.vaccine.pcv13>
	having uuid {5dda76d6-c192-4260-90cc-bbd96b790656}
	having status active
	with metadata
		author SanteSuite Inc. and the SanteSuite Contributors
		version 3.05
		doc This library contains the dosing logic for PCV13 immunizations
	end metadata
	as
	define logic "PCV13 Dose Schedule Logic"
		having id <org.santedb.cdss.vaccine.pcv13.patient>
		having status active
		having context Patient when all(
				"Patient Is A Child",
				"Patient Is Not Deceased"
			)

		as
		define fact "Patient's Last Administration of PCV13 Dose 1"
		as
			query(
				from hdsi($$participation[RecordTarget]$$)
				where hdsi($$
          source.isNegated=false&
          source.participation[Product].player.typeConcept.mnemonic=VaccineType-PCV13&
          source@SubstanceAdministration.doseSequence=1
          $$)
				select last hdsi($$source@SubstanceAdministration$$)
				order by hdsi($$source.actTime$$)
			)

		end fact
		define fact "Patient's Last Administration of PCV13 Dose 2"
		as
			query(
				from hdsi($$participation[RecordTarget]$$)
				where hdsi($$
          source.isNegated=false&
          source.participation[Product].player.typeConcept.mnemonic=VaccineType-PCV13&
          source@SubstanceAdministration.doseSequence=2
          $$)
				select last hdsi($$source@SubstanceAdministration$$)
				order by hdsi($$source.actTime$$)
			)

		end fact
		define fact "Patient's Last Administration of PCV13 Dose 3"
		as
			query(
				from hdsi($$participation[RecordTarget]$$)
				where hdsi($$
          source.isNegated=false&
          source.participation[Product].player.typeConcept.mnemonic=VaccineType-PCV13&
          source@SubstanceAdministration.doseSequence=3
          $$)
				select last hdsi($$source@SubstanceAdministration$$)
				order by hdsi($$source.actTime$$)
			)

		end fact
		define fact "Patient Has an Unresolved AEFI to PCV13 Dose"
			with metadata
				doc Patient has an active (unresolved) AEFI to a previous dose of PCV13
			end metadata
		as
			any(
				hdsi($$
            participation[RecordTarget].source.typeConcept=0744B6AD-BE39-4A08-B64D-F61CB8282267
            &participation[RecordTarget].source.relationship[HasSubject].target.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d
            &participation[RecordTarget].source.relationship[RefersTo].target.participation[Product].player.typeConcept.mnemonic=VaccineType-PCV13
          $$),
				hdsi($$
            participation[RecordTarget].source.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d&
            participation[RecordTarget].source@CodedObservation.value.mnemonic=VaccineType-PCV13&
            participation[RecordTarget].source.isNegated=false
          $$)
			)

		end fact
		define fact "Patient Refused Previous PCV13"
			with metadata
				doc When a patient has refused a previous dose of PCV13 due to personal or religious reasons
			end metadata
		as
			hdsi($$
          participation[RecordTarget].source.isNegated=true&
          participation[RecordTarget].source.participation[Product].player.typeConcept.mnemonic=VaccineType-PCV13&
          participation[RecordTarget].source.reasonConcept.conceptSet=3BD46185-E3C3-4A6D-A1FE-0F2C9C49360D
        $$)
		end fact
		define model "PCV Dose Administration Proposal"
			having format json
		as
			$$

          {
				      "$type": "SubstanceAdministration",
               "template":"50ac9b2d-e560-4b75-ac77-921bf0eceee8",
              "templateModel" : {
                "mnemonic" : "org.santedb.emr.sbadm.immunization",
                "oid": "1.3.5.1.4.1.52820.5.2.3.2"
              },
              "typeConcept": "6e7a3521-2967-4c0a-80ec-6c5c197b2178",
              "doseQuantity" : 1.0,
              "doseUnit" : "a4fc5c93-31c2-4f87-990e-c5a4e5ea2e76",
              "route" : "D594F99F-0151-41A0-A359-282AB54683A1",
			  "site":"6d607c13-5a87-40b2-91cd-79f000bd82e4",
			  "participation": {
				"Product": [{
                	"player" : "e829c3d1-5243-474e-a2d6-ba35d99610c4"
						}]
					}
				}
          
			$$
		end model
		define fact "Patient Should Receive PCV13 Dose 1"
		as
			none(
				"Patient's Last Administration of PCV13 Dose 1",
				"Patient Refused Previous PCV13"
			)

		end fact
		define fact "Patient Should Receive PCV13 Dose 2"
		as
			all(
				"Patient's Last Administration of PCV13 Dose 1",
				none(
					"Patient's Last Administration of PCV13 Dose 2",
					"Patient Refused Previous PCV13"
				)

			)

		end fact
		define fact "Patient Should Receive PCV13 Dose 3"
		as
			all(
				"Patient's Last Administration of PCV13 Dose 2",
				none(
					"Patient's Last Administration of PCV13 Dose 3",
					"Patient Refused Previous PCV13"
				)
			)

		end fact
		define rule "Raise Alert when Patient is to Receive PCV13 but has an AEFI"
		as
			when
				all(
					any(
						"Patient Should Receive PCV13 Dose 1",
						"Patient Should Receive PCV13 Dose 2",
						"Patient Should Receive PCV13 Dose 3"
					),
					"Patient Has an Unresolved AEFI to PCV13 Dose"
				)

			then
				raise having priority warn 
					having type {1a4fff6c-f54f-11e8-8eb2-f2801f1b9fd1}
				    $$Patient has an unresolved AEFI and/or drug allergy/intolerance to PCV13 Reported - Please consult with patient prior to administration of PCV13$$
		end rule
		define rule "Recommend PCV13 Dose 1"
		as
			when
				"Patient Should Receive PCV13 Dose 1"
			then
				propose  having model "PCV Dose Administration Proposal"
				as
					assign csharp($$context.GreaterOf(DateTime.Now, context.Target.DateOfBirth.Value.AddDays(42))$$) to actTime
					assign csharp($$context.Target.DateOfBirth.Value.AddDays(42)$$) to startTime
					assign const "1" to doseSequence
					assign const "F3BE6B88-BC8F-4263-A779-86F21EA10A47" to typeConcept overwrite
				end propose
		end rule
		define rule "Recommend PCV13 Dose 2"
		as
			when
				"Patient Should Receive PCV13 Dose 2"
			then
				propose  having model "PCV Dose Administration Proposal"
				as
					assign csharp($$((Act)context["Patient's Last Administration of PCV13 Dose 1"]).ActTime.Value.AddDays(28)$$) to actTime
					assign hdsi($$actTime$$ scoped-to proposal) to startTime
					assign const "2" to doseSequence
				end propose
		end rule
		define rule "Recommend PCV13 Dose 3"
		as
			when
				"Patient Should Receive PCV13 Dose 3"
			then
				propose  having model "PCV Dose Administration Proposal"
				as
					assign csharp($$((Act)context["Patient's Last Administration of PCV13 Dose 2"]).ActTime.Value.AddDays(28)$$) to actTime
					assign hdsi($$actTime$$ scoped-to proposal) to startTime
					assign const "3" to doseSequence
				end propose
		end rule
		define protocol "PCV13 Childhood Immunization Schedule"
			having id <org.santedb.cdss.vaccine.pcv13.protocol>
			having uuid {5dda76d6-c192-4260-90cc-bbd96b790656}
			having oid "1.3.5.1.4.1.52820.5.2.3.2.4"
			having scope <org.santedb.ims.pediatric.routineVacc>
		as
			when
				"Patient Is A Child"
			then
				apply "Recommend PCV13 Dose 1"
				apply "Recommend PCV13 Dose 2"
				apply "Recommend PCV13 Dose 3"
		end protocol
	end logic
end library
