//
// Copyright (C) 2021 - 2023, SanteSuite Inc. and the SanteSuite Contributors (See NOTICE.md for full copyright notices)
// Copyright (C) 2019 - 2021, Fyfe Software Inc. and the SanteSuite Contributors
// Portions Copyright (C) 2015-2018 Mohawk College of Applied Arts and Technology
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you 
// may not use this file except in compliance with the License. You may 
// obtain a copy of the License at 
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the 
// License for the specific language governing permissions and limitations under 
// the License.
//
include <org.santedb.cdss.core>
include <org.santedb.cdss.core.childcare>

define library "Inactivated Poliovirus Vaccine (IPV)"
	having id <org.santedb.cdss.vaccine.ipv>
	having uuid {dbb50d60-ed0f-11ee-bda3-07ac3f87ee47}
	having oid "1.3.5.1.4.1.52820.5.2.3.2.8"
	having status active
	with metadata
		version 3.05
	end metadata
	as
	
	define logic "IPV Vaccination Logic for Patient" 
	    having id <org.santedb.cdss.vaccine.ipv.patient>
	    having status active
	    having context Patient when all(
	        "Patient Is a Child",
	        "Patient Is Not Deceased"
	    )
	    as 
	    
	    define fact "Patient's Last Administration of IPV Dose 1" 
	    with metadata
	        doc The last administration of IPV administered (or proposed) to the Patient
	    end
	    as
	        query(
	            from hdsi($$participation[RecordTarget]$$)
	            where hdsi($$
	                source@SubstanceAdministration.doseSequence=1&
	                source.participation[Product].player.typeConcept.mnemonic=VaccineType-IPV&
	                source.isNegated=false
	           $$)
	           select last hdsi($$source@SubstanceAdministration$$)
	           order by hdsi($$source.actTime$$)
	       )
	    end fact
	    
	    define fact "Patient Has an Unresolved AEFI to IPV Dose" as 
	        any(
	            hdsi($$
	                participation[RecordTarget].source.typeConcept=0744B6AD-BE39-4A08-B64D-F61CB8282267&
	                participation[RecordTarget].source.relationship[HasSubject].target.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d&
	                participation[RecordTarget].source.relationship[RefersTo].target.participation[Product].player.typeConcept.mnemonic=VaccineType-IPV
	            $$),
	            hdsi($$
	                participation[RecordTarget].source.typeConcept=0124fde0-7857-4815-b257-74acaa0dd92d&
	                participation[RecordTarget].source@CodedObservation.value.mnemonic=VaccineType-IPV&
	                participation[RecordTarget].source.isNegated=false
	            $$)
	        )
	    end fact
	   
	   define fact "Patient Refused Previous IPV Dose" as 
	        hdsi($$
	            participation[RecordTarget].source.isNegated=true&
	            participation[RecordTarget].source.participation[Product].player.typeConcept.mnemonic=VaccineType-IPV&
	            participation[RecordTarget].source.reasonConcept.conceptSet=3BD46185-E3C3-4A6D-A1FE-0F2C9C49360D
	           $$)
	   end fact 
	   
	   define fact "Patient Should Receive IPV Dose 1" as 
	    none("Patient's Last Administration of IPV Dose 1", "Patient Refused Previous IPV Dose")
	   end fact 
	   
	   define rule "Raise Alert if Active AEFI Reported for IPV" as 
	    when all(
	        any("Patient Should Receive IPV Dose 1"), 
	        "Patient Has an Unresolved AEFI to IPV Dose"
	   ) 
	   then 
	    raise having priority warn having type {1a4fff6c-f54f-11e8-8eb2-f2801f1b9fd1} $$
	        Patient has had a previous adverse event and/or drug intolerance to an IPV dose - confirm administration with patient/guardian prior to administration
	       $$ 
	   end rule
	   
	   define model "IPV Dose Proposal" having format json as 
	   $$
        {
        	"$type": "SubstanceAdministration",
            "template":"50ac9b2d-e560-4b75-ac77-921bf0eceee8",
            "templateModel" : {
                "mnemonic" : "org.santedb.emr.sbadm.immunization",
                "oid": "1.3.5.1.4.1.52820.5.2.3.2"
              },
              "typeConcept": "6e7a3521-2967-4c0a-80ec-6c5c197b2178",
              "doseQuantity" : 1.0,
              "doseUnit" : "a4fc5c93-31c2-4f87-990e-c5a4e5ea2e76",
              "route" : "d594f99f-0151-41a0-a359-282ab54683a1",
              "site" : "dd5db8ed-0d97-4728-bd94-27aacd79ea02",
        	  "participation": {
        		"Product": [{
                	"player" : "800b8fc3-a2f9-4f24-a119-6e98b76119a1",
					"playerModel" : {
						"$type": "Material",
						"id" : "800b8fc3-a2f9-4f24-a119-6e98b76119a1",
						"typeConceptModel": {
							"mnemonic": "VaccineType-IPV"
						}
					}
        		}]
        	}
        }	   
	   $$ 
	   end model
	    
	   define rule "Recommend Administration of IPV Dose 1" as 
	    when "Patient Should Receive IPV Dose 1"
	    then 
	        propose having model "IPV Dose Proposal" as 
				assign csharp($$context.GreaterOf(DateTime.Now, context.Target.DateOfBirth.Value.AddDays(98))$$) to actTime
	            assign csharp($$context.Target.DateOfBirth.Value.AddDays(98)$$) to startTime
	            assign const 1 to doseSequence
	        end propose
	   end rule
	        
	   define protocol "IPV Administration to Children" 
	    having id <org.santedb.cdss.vaccine.ipv.protocol>
	    having uuid {64a9a404-76e2-4a48-8701-7b71d4759ee3}
	    having oid "1.3.5.1.4.1.52820.5.2.3.2.8.1"
	    having scope <org.santedb.ims.pediatric.routineVacc>
	    as 
	        when "Patient Is a Child"
	        then 
	            apply "Recommend Administration of IPV Dose 1"
	   end protocol
	end logic
end library
